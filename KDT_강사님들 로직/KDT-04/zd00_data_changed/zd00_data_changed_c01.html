<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html>
<head>
<title>ZD00_DATA_CHANGED_C01</title>
</head>
<body bgcolor="#FFFFE0">
<font size="3" face = "Arial" color="#000000"><b>Code listing for: ZD00_DATA_CHANGED_C01</b></font>
<br>
<font size="3" face = "Arial" color="#000000"><b>Description:  Include ZD00_DATA_CHANGED_C01</b></font>
<hr>
<pre width="100">
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& Include          ZD00_DATA_CHANGED_C01</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">************************************************************************</font>
<font color ="#0000FF">* LOCAL CLASS 정의</font>
<font color ="#0000FF">************************************************************************</font>
CLASS lcl_event_receiver DEFINITION.

  PUBLIC SECTION.

    METHODS handle_data_changed
      FOR EVENT data_changed OF cl_gui_alv_grid
      IMPORTING er_data_changed.

  PRIVATE SECTION.    "변경할 값에 대한 유효성 체크

    DATA : error_in_data TYPE c. "오류발생시 체크하는 플래그 변수 -&gt; 프로토콜 메시지 창에 보여줄 조건

    METHODS check_planetype
      IMPORTING
        ps_good_planetype TYPE lvc_s_modi
        pr_data_changed   TYPE REF TO cl_alv_changed_data_protocol.

    METHODS ch_new_plane_v_new_seatsocc
      IMPORTING
        psg_plane       TYPE lvc_s_modi
        psg_socc        TYPE lvc_s_modi
        ps_saplane      TYPE saplane
        pr_data_changed TYPE REF TO cl_alv_changed_data_protocol.

    METHODS ch_new_plane_v_old_seatsocc
      IMPORTING
        psg_plane       TYPE lvc_s_modi
        ps_saplane      TYPE saplane
        pr_data_changed TYPE REF TO cl_alv_changed_data_protocol.

    METHODS check_seatsocc
      IMPORTING
        ps_good         TYPE lvc_s_modi
        pr_data_changed TYPE REF TO cl_alv_changed_data_protocol.

ENDCLASS.
<font color ="#0000FF">************************************************************************</font>
<font color ="#0000FF">* LOCAL CLASS 구현</font>
<font color ="#0000FF">************************************************************************</font>
CLASS lcl_event_receiver IMPLEMENTATION.

  METHOD handle_data_changed.

    DATA : ls_good TYPE lvc_s_modi.

    error_in_data = space.

    " 각 row에 해당하는 필드의 값이 변동되었을 때 확인 (딕셔너리에 정의된 데이터 유형이나 길이, 값 범위)
    " mt_good_cells : 변경셀을 인식해서 임시 저장공간에 넣어두고 값 비교 가능
    LOOP AT er_data_changed-&gt;mt_good_cells INTO ls_good.

      CASE ls_good-fieldname.

        WHEN 'PLANETYPE'.
          CALL METHOD check_planetype
            EXPORTING
              ps_good_planetype = ls_good
              pr_data_changed   = er_data_changed.

        WHEN 'SEATSOCC'.
          CALL METHOD check_seatsocc
            EXPORTING
              ps_good         = ls_good
              pr_data_changed = er_data_changed.

      ENDCASE.

    ENDLOOP.

    " 플래그에 표기되어있으면 (즉, 오류발생 시) 프로토콜 메세지창에 해당 오류 로그 보여준다.
    IF error_in_data EQ 'X'.
      CALL METHOD er_data_changed-&gt;display_protocol.
    ENDIF.

  ENDMETHOD.
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& 변경된 값의 기종이 존재하는 지 확인합니다.</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
  METHOD check_planetype.

    DATA : lv_planetype     TYPE s_planetye,
           ls_saplane       TYPE saplane,
           ls_good_seatsocc TYPE lvc_s_modi.

    " 변경된 셀 값 인지
    CALL METHOD pr_data_changed-&gt;get_cell_value
      EXPORTING
        i_row_id    = ps_good_planetype-row_id
        i_fieldname = ps_good_planetype-fieldname
      IMPORTING
        e_value     = lv_planetype.  "변경된 값 반환

<font color ="#0000FF">* 1. 해당 비행기 유형이 존재하는가? (비행기 기종 관리하는 마스터에서 확인)</font>

    SELECT SINGLE * FROM saplane
                    INTO ls_saplane
                   WHERE planetype = lv_planetype.

    " 존재 유무에 따른 메세지 팝업 생성 -&gt; 존재하지 않으면 결과 주고 exit
    IF sy-subrc NE 0.

      CALL METHOD pr_data_changed-&gt;add_protocol_entry "프로토콜 메세지창에 메세지 등록
        EXPORTING
          i_msgid     = '0K'
          i_msgno     = '000'
          i_msgty     = 'E'
          i_msgv1     = TEXT-m03
          i_msgv2     = lv_planetype
          i_msgv3     = TEXT-m05
          i_fieldname = ps_good_planetype-fieldname
          i_row_id    = ps_good_planetype-row_id.

      error_in_data = 'X'.  "오류 발생했다. 표기함
      EXIT.

    ENDIF.

    " 존재하면... 2. 최대 좌석수가 예약 좌석수에 충족하는가? 확인

    READ TABLE pr_data_changed-&gt;mt_good_cells INTO ls_good_seatsocc
                       WITH KEY row_id    = ps_good_planetype-row_id
                                fieldname = 'SEATSOCC'.
    IF sy-subrc = 0.

      CALL METHOD ch_new_plane_v_new_seatsocc
        EXPORTING
          psg_plane       = ps_good_planetype
          psg_socc        = ls_good_seatsocc
          ps_saplane      = ls_saplane
          pr_data_changed = pr_data_changed.

    ELSE.

      CALL METHOD ch_new_plane_v_old_seatsocc
        EXPORTING
          psg_plane       = ps_good_planetype
          ps_saplane      = ls_saplane
          pr_data_changed = pr_data_changed.

    ENDIF.

  ENDMETHOD.                           " CHECK_PLANETYPE
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& 새로운 기종 선택에 따른 신규 예약 좌석 수 확인</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
  METHOD ch_new_plane_v_new_seatsocc.

    DATA : lv_seatsocc TYPE s_seatsocc.

    " 변경된 값을 셀에서 인식

    CALL METHOD pr_data_changed-&gt;get_cell_value
      EXPORTING
        i_row_id    = psg_socc-row_id
        i_fieldname = psg_socc-fieldname
      IMPORTING
        e_value     = lv_seatsocc.

    IF ps_saplane-seatsmax GE lv_seatsocc.

      " 값이 유효한 경우 다른 셀의 값을 변경할 수 있습니다.
      CALL METHOD pr_data_changed-&gt;modify_cell
        EXPORTING
          i_row_id    = psg_plane-row_id
          i_fieldname = 'SEATSMAX'
          i_value     = ps_saplane-seatsmax.
    ELSE.

      " 값 유효하지 않으면 프로토콜 항목 생성해서 알려주기.

      CALL METHOD pr_data_changed-&gt;add_protocol_entry
        EXPORTING
          i_msgid     = '0K'
          i_msgno     = '000'
          i_msgty     = 'E'
          i_msgv1     = TEXT-m03
          i_msgv2     = ps_saplane-planetype
          i_msgv3     = TEXT-m04
          i_fieldname = psg_plane-fieldname
          i_row_id    = psg_plane-row_id.

      error_in_data = 'X'.
    ENDIF.

  ENDMETHOD.
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& 새로운 기종 선택에 따른 이전 예약 좌석 수 확인</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
  METHOD ch_new_plane_v_old_seatsocc.

    DATA: lv_old_seatsocc TYPE s_seatsocc,
          ls_outtab       TYPE sflight.

    " 이전값 확인
    READ TABLE gt_outtab INTO ls_outtab INDEX psg_plane-row_id.
    lv_old_seatsocc = ls_outtab-seatsocc.

    IF ps_saplane-seatsmax GE lv_old_seatsocc.

      CALL METHOD pr_data_changed-&gt;modify_cell
        EXPORTING
          i_row_id    = psg_plane-row_id
          i_fieldname = 'SEATSMAX'
          i_value     = ps_saplane-seatsmax.
    ELSE.
      CALL METHOD pr_data_changed-&gt;add_protocol_entry
        EXPORTING
          i_msgid     = '0K'
          i_msgno     = '000'
          i_msgty     = 'E'
          i_msgv1     = TEXT-m03
          i_msgv2     = ps_saplane-planetype
          i_msgv3     = TEXT-m04
          i_fieldname = psg_plane-fieldname
          i_row_id    = psg_plane-row_id.

      error_in_data = 'X'.
    ENDIF.
  ENDMETHOD.
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
<font color ="#0000FF">*& 변경행에 해당되는 기종의 예약 좌석 수 확인</font>
<font color ="#0000FF">*&---------------------------------------------------------------------*</font>
  METHOD check_seatsocc.

    DATA : lv_seatsocc     TYPE s_seatsocc,
           lv_old_seatsmax TYPE s_seatsmax,
           ls_outtab       TYPE sflight,
           ls_good         TYPE lvc_s_modi.

    " 비행기 기종 변경 값 확인
    READ TABLE pr_data_changed-&gt;mt_good_cells INTO ls_good
                       WITH KEY row_id    = ps_good-row_id
                                fieldname = 'PLANETYPE'.
    IF sy-subrc EQ 0.
      EXIT.
    ENDIF.

<font color ="#0000FF">* 변경된 셀 값 가져오기</font>
    CALL METHOD pr_data_changed-&gt;get_cell_value
      EXPORTING
        i_row_id    = ps_good-row_id
        i_fieldname = ps_good-fieldname
      IMPORTING
        e_value     = lv_seatsocc.

<font color ="#0000FF">* 이전 값</font>
    READ TABLE gt_outtab INTO ls_outtab INDEX ps_good-row_id.
    lv_old_seatsmax = ls_outtab-seatsmax.


    IF lv_seatsocc &gt; lv_old_seatsmax.  "최대좌석수를 넘으면 안되겠쥬?

      CALL METHOD pr_data_changed-&gt;add_protocol_entry "프로토콜 창에 보여주기 위해 등록
        EXPORTING
          i_msgid     = '0K'
          i_msgno     = '000'
          i_msgty     = 'E'
          i_msgv1     = TEXT-m01
          i_msgv2     = TEXT-m02
          i_msgv3     = ls_outtab-planetype
          i_fieldname = ps_good-fieldname
          i_row_id    = ps_good-row_id.

      error_in_data = 'X'. "에러 발생했으니 플래그에 표기.

    ENDIF.

  ENDMETHOD.                           " CHECK_SEATSOCC

ENDCLASS.
</pre>
<hr>
<font size="2" face = "Sans Serif">Extracted by Direct Download Enterprise version 1.3.1 - E.G.Mellodew. 1998-2005 UK. Sap Release 754
</font>
</body>
</html>
